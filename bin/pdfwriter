#!/usr/bin/env raku

use PDF::API6;
use PDF::Page;
use PDF::Content::Page :PageSizes;
use PDF::Content::Font::CoreFont;
constant CoreFont = PDF::Content::Font::CoreFont;

use Text::Utils :ALL;
use Config::TOML;

use lib <./lib>;
use PDF::Writer;

my %config; # toml hash
my $cfil = "{%*ENV<HOME>}/.pdfwriter/default.toml";

my $usage = 0;
if not @*ARGS.elems {
    say qq:to/HERE/;
    Usage: {$*PROGRAM.IO.basename} mode [options...] <input text file>

    Modes
      text - Converts the input file's lines to PDF (default)
      pod  - Converts the Raku pod file to PDF using :config
               options. See pod examples in the examples directory.

    Options
      number          - Add line numbers in the left column
      underline       - Stroke each baseline with a thin line
      title[=T]       - Add title to the page number line (default: input file name)
      size            - Font size (default: 10)
      font            - Font name (default: Courier)
      margins=l,r,t,b - Margins (default: one-inch on all sides)
      paper=T         - Where T is Letter (default), Legal, A4, or A5
      truncate=N      - Where N is the max number of chars on a line
                          and the rest are ignored
      wrap=N          - Where N is the max number of chars on a line
                          and the rest are wrapped to as many lines
                          as needed
      config=F        - Where F is the name of a TOML file other than
                          the default location
      verbose         - See more etails of the execution
      debug           - For developer use

    Notes
      Modes and options may be selected by the minimum number of unique
        leading characters, e.g., 'te' selects 'text' and 'tr' selects
        'truncate'.
      You may enter personal defaults in a TOML file defined at:
        '\$HOME/.pdfwriter/default.toml'
    HERE
    exit
}

my $ifil = @*ARGS.pop;
if not ($ifil.IO.f and $ifil.IO.r) {
    die "FATAL: Input file '$ifil' cannot be read.";
}

#enum Paper <Letter Legal A4 A5>; # this enum is already defined in PDF::API6

my $text      = 1;
my $pod       = 0;
my $debug     = 0;
my $verbose   = 0;
my $toml;

my $doc = Doc.new;


for @*ARGS {
    # options
    # doc options (may be defined in the config file)
    when $_.contains(/:i ^u/) { $doc.underline = 1 }
    when $_.contains(/:i ^n/) { $doc.number = 1 }
    when /:i tr <[uncate]>* '=' (\d+) / {
        $doc.truncate = +$0;
    }
    when /:i w <[rap]>* '=' (\d+) / {
        $doc.wrap = +$0;
    }
    when /:i pa <[per]>* '=' (\S+) / {
        $doc.paper = ~$0;
    }
    when /:i st <[tyle]>* '=' (\S+) / {
        $doc.style = ~$0;
    }
    when /:i si <[ze]>* '=' (\S+) / {
        $doc.size = ~$0;
    }
    when /:i w <[eight]>* '=' (\S+) / {
        $doc.weight = ~$0;
    }
    when /:i ti <[tle]>* ['=' (\S+)]? / {
        if $0.defined {
            $doc.title = ~$0;
        }
        else {
            $doc.title = $ifil;
        }
    }
    when /:i m <[argins]>* '=' (\S+) ',' (\S+) ',' (\S+) ',' (\S+) / {
        $doc.left   = +$0;
        $doc.right  = +$1;
        $doc.top    = +$2;
        $doc.bottom = +$3;
    }

    # other options
    when $_.contains(/:i ^v/) { $verbose = 1 }
    when $_.contains(/:i ^d/) { $debug = 1 }
    when /:i c <[onfig]>* '=' (\S+) / {
        $toml = ~$0;
    }

    # modes
    when $_.contains(/:i ^t/) { $text = 1; $pod = 0 }
    when $_.contains(/:i ^p/) { $pod = 1; $text = 0 }

    default { die "FATAL: Unknown arg '$_'" }
}

if $cfil.IO.r {
    %config = from-toml :file($cfil);
    note "Found your 'default.toml' file." if $debug;
    if $debug {
        note "DEBUG: Dumping \%config:";
        note %config.raku;
        note "DEBUG early exit";
        exit;
    }
    # extract and set vars
    my $err = 0;
    my $def = %config<default> // ++$err;
    if $err {
        note "WARNING: Cannot find config key: default.";
        note "         You may have unexpected results.";
        exit;
    }
    my %h = %config{$def};
    for %h.kv -> $k, $v {
        note "  $k => '$v'" if $debug;
        given $k {
            when $k eq 'underline' { $doc.underline = $v }
            when $k eq 'leading-ratio' { $doc.leading-ratio = $v }
            when $k eq 'size' { $doc.size = $v }
            when $k eq 'font' { $doc.font = $v }
            default { ++$err; note "Unknown config key: $k" }
        }
    }
    if $err {
        note "WARNING: One or more unknown config keys were found.";
        note "         You may have unexpected results.";
    }
}
else {
    note "No 'default.toml' file was found in your home directory, subdir '.pdfwriter'."
}

if $pod {
   die "FATAL: The pod mode is not yet implemented.";
}

# some default settings to get started
# TODO define a page class to hold such things
$doc.height = 11  * 72;
$doc.width  = 8.5 * 72;

$doc.Pdf = PDF::API6.new;

if $ifil ~~ /:i pdf $/ {
    die "FATAL: Not attempting to read a file with name ending in 'pdf': $ifil ";
}
$doc.title = $ifil;


my $ofil = $ifil ~ '.pdf';
if $debug {
   say "DEBUG: infile '$ifil'; outfile '$ofil'";
   exit;
}

# GLOBAL VALUES =====================================
# Set the default page size for all pages
$doc.Pdf.media-box = $doc.paper;


#$doc.Font = $doc.Pdf.core-font(:family<Courier>); #, :weight<Bold>;
my $Font = $doc.Pdf.core-font(:family($doc.font)); #, :weight<Bold>;
my $Size = $doc.size;

# default size

my @lines = $ifil.IO.lines;

text2pdf @lines, :$doc, :$Font, :$Size, :$debug;

# Save the new PDF
$doc.Pdf.save-as($ofil);
say "See file '$ofil'";

##### subroutines #####

=finish
# time trials showed no significant time savings by processing a page of text for four-page docs
sub make-text-by-pages($pdf, @lines, :$debug) {
    # page-by-page (an optimization)

    # determine how many pages:
    my $lines-per-page = (($height - ($top - $bottom)) / $line-spacing).floor;
    my $npages = (@lines.elems / $lines-per-page).ceiling;

    my @pages;
    my $str = '';

    my $pn     = 0;
    my $nlines = 0;
    for @lines -> $line {
        ++$nlines;
        if $nlines > $lines-per-page {
            # put the string of page text away
            @pages.push: $str;
            ++$pn;
            # reinitialize
            $nlines = 0;
            $str = '';
        }
        $str ~= $line;
    }
    if $str {
        @pages.push: $str;
    }
    if @pages.elems != $npages {
        die "FATAL: \$npages ($npages) != \@pages.elems ({@pages.elems})";
    }

    # now convert the pages of text to PDF

    # Add a blank page to start
    my $page = $pdf.add-page();
    my $x  = $left;
    my $y0 = $height - $top - $line-spacing;
    my $y  = $y0;
    my $pnum      = 1; # for page numbering and control
    my $pnumbered = 0; # for page numbering and control
    for @lines -> $line {
        # add the line's text to the page
        $page.text: {
            .font = $doc.Font, $size;
            .text-position = $x, $y;
            .say($line);
        }

        $y -= $line-spacing;
        if $y <= $bottom {
            note "DEBUG: numbering page $pnum of $npages" if $debug;
            # add a page number
            ++$pnumbered;
            my $pp = "Page $pnum of $npages";
            my $yy = 36; # 1/2 inches from the bottom
            $page.text: {
                .font = $doc.Font, $size;
                .text-position = $x, $yy;
                .say($pp);
            }

            # start a new page
            $page = $pdf.add-page();
            ++$pnum;
            # reset y
            $y  = $y0;
        }
    }
    # make sure the last page is numbered

    note "DEBUG: finished page $pnum of $npages" if $debug;
    note "DEBUG: actually numbered $pnumbered pages" if $debug and $pnumbered != $npages;
    if $pnum <= $npages and $pnumbered != $npages {
        # add a page number
        ++$pnumbered;
        my $pp = "Page $pnumbered of $npages";
        my $yy = 36; # 1/2 inches from the bottom
        $page.text: {
            .font = $doc.Font, $size;
            .text-position = $x, $yy;
            .say($pp);
        }
    }
    note "DEBUG: have now numbered $pnumbered pages" if $debug and $pnumbered == $npages;

} # make-text-by-pages
